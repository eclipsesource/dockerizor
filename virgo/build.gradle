ext.virgoProjects = [
    project(':vts'),
    project(':vjs'),
    project(':vrs')
]

buildscript {
    repositories {
        if (System.properties['use.local.dockerizor.snapshot'] == 'true') {
            mavenLocal()
        }
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
    }
    dependencies {
        if (System.properties['use.local.dockerizor.snapshot'] == 'true') {
            classpath 'com.eclipsesource.dockerizor:dockerizor:0.7.1.BUILD-SNAPSHOT'
        } else {
            classpath 'gradle.plugin.com.eclipsesource.dockerizor:dockerizor:0.7.0'
        }
    }
}

configure(virgoProjects) {
    apply plugin: 'java'
    apply plugin: 'eclipse'

    compileJava {
        sourceCompatibility = '1.7'
        targetCompatibility = '1.7'
    }

    eclipse {
        jdt {
            sourceCompatibility = '1.7'
            targetCompatibility = '1.7'
        }
    }

    ext { rapVersion = '2.3.0' }

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

configure(virgoProjects) {
    apply plugin: 'com.eclipsesource.dockerizor'

    dockerizor {
        maintainer = 'Florian Waibel <fwaibel@eclipsesource.com>'
        tag = System.getenv("dockerizor.tag") ?: 'latest'

        createLocalCopy = System.properties['dockerizor.createLocalCopy'] ?: false
        removeAdminConsole = System.properties['dockerizor.removeAdminConsole'] ?: true
        removeSplash = System.properties['dockerizor.removeSplash'] ?: true

        virgoVersion = System.getenv("dockerizor.virgoVersion") ?: 'latest'
    }
}

task wrapper(type: Wrapper) { gradleVersion = '2.14' }
