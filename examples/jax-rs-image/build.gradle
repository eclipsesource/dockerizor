import com.eclipsesource.dockerizor.DockerizorTask

apply plugin: 'base'
apply plugin: 'com.eclipsesource.dockerizor'

dockerizor {
    repository = 'dockerizor-example/jax-rs'
    maintainer = 'Florian Waibel <fwaibel@eclipsesource.com>'
    description = 'Virgo JAX-RS examples created with Gradle Plugin: com.eclipsesource.dockerizor'

    javaImage = 'java:openjdk-7u95-jre'

    createLocalCopy = true

    virgoFlavour = 'VJS'
    removeAdminConsole = false
    removeSplash = false

    pickupFiles = ['jax-rs-example.plan']

    postDockerizeHook = { task ->
        logger.info "Adding JavaSE-1.7 to configuration/java6-server.profile"
        task.RUN "sed -i 's/JavaSE-1.6/JavaSE-1.6,\\\\\\n JavaSE-1.7/' ${project.dockerizor.virgoHome}/configuration/java6-server.profile"
        logger.info "Changing org.osgi.service.http.port=8080 to 9090"
        task.RUN "sed -i 's/8080/9090/' ${project.dockerizor.virgoHome}/configuration/config.ini"
        task.EXPOSE 9090
        task.EXPOSE 8080

        logger.info "Adding OSGi dependencies not available via standard dependency mechanism:"
        logger.info "Getting oee.http.jetty/oee.http.servlet from eclipse archive"
        // http://archive.eclipse.org/equinox/drops/R-3.8.2-201302041200/org.eclipse.equinox.http.jetty_3.0.1.v20121109-203239.jar
        // http://archive.eclipse.org/equinox/drops/R-3.8.2-201302041200/org.eclipse.equinox.http.servlet_1.1.300.v20120912-130548.jar
        String eclipseArchiveUrl = 'http://archive.eclipse.org/equinox/drops/R-3.8.2-201302041200'
        String httpJetty = 'org.eclipse.equinox.http.jetty_3.0.1.v20121109-203239.jar'
        String httpServlet = 'org.eclipse.equinox.http.servlet_1.1.300.v20120912-130548.jar'
        task.RUN ("curl -L -o ${virgoHome}/repository/ext/${httpJetty} ${eclipseArchiveUrl}/${httpJetty}")
        task.RUN ("curl -L -o ${virgoHome}/repository/ext/${httpServlet} ${eclipseArchiveUrl}/${httpServlet}")

        logger.info "Getting gson from eclipse orbit"
        // http://download.eclipse.org/tools/orbit/downloads/drops/R20151221205849/repository/plugins/com.google.gson_2.2.4.v201311231704.jar
        String orbitMars2Url = 'http://download.eclipse.org/tools/orbit/downloads/drops/R20151221205849/repository/plugins'
        String gson = 'com.google.gson_2.2.4.v201311231704.jar'
        task.RUN ("curl -L -o ${virgoHome}/repository/ext/${gson} ${orbitMars2Url}/${gson}")
    }

    dependencies {
        repositoryExt 'com.eclipsesource.jaxrs:jersey-all:2.10.1'
        repositoryExt 'com.eclipsesource.jaxrs:publisher:4.1'
        repositoryExt 'com.eclipsesource.jaxrs:provider-gson:2.0'

        repositoryUsr project(':jax-rs')
    }

}
